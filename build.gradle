plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = 'ro.ghionoiu'
version = project.version.toString()

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'ro.ghionoiu:tdl-client-java:0.26.5'
    implementation 'com.google.code.gson:gson:2.8.9'

    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
    testImplementation('org.junit.platform:junit-platform-suite:1.8.1')
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

application {
    mainClass = 'befaster.SendCommandToServer'
}

run {
    standardInput = System.in

    def action = System.getProperty('action')
    def actions = (action == null) ? [] : [ action ]
    //noinspection GroovyAssignabilityCheck
    args(actions)
}

test {
    useJUnitPlatform()
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    testLogging {
        events "passed", "skipped", "failed"
    }

    ignoreFailures = true
    finalizedBy jacocoTestReport
}

jacoco {
    reportsDirectory = layout.buildDirectory.dir('jacoco')
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}
